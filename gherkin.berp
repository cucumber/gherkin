[
	Tokens -> #Empty,#Comment,#TagLine,#FeatureLine,#RuleLine,#BackgroundLine,#ScenarioLine,#ExamplesLine,#StepLine,#DocStringSeparator,#TableRow,#Language
	IgnoredTokens -> #Comment,#Empty
	ClassName -> Parser
	Namespace -> Gherkin
]

GherkinDocument! := Feature?
Feature! := FeatureHeader Background? ScenarioDefinition* Rule*
FeatureHeader! := #Language? Tags? #FeatureLine DescriptionHelper

Rule! := RuleHeader Background? ScenarioDefinition*
RuleHeader! := Tags? #RuleLine DescriptionHelper

Background! := #BackgroundLine DescriptionHelper Step*

// Interpreting a tag line is ambiguous (tag line of rule or of scenario)
ScenarioDefinition! [#Empty|#Comment|#TagLine->#ScenarioLine]:= Tags? Scenario

Scenario! := #ScenarioLine DescriptionHelper Step* ExamplesDefinition*
// after the first "Data" block, interpreting a tag line is ambiguous (tagline of next examples or of next scenario)
// because of this, we need a lookahead hint, that connects the tag line to the next examples, if there is an examples block ahead
ExamplesDefinition! [#Empty|#Comment|#TagLine->#ExamplesLine]:= Tags? Examples
Examples! := #ExamplesLine DescriptionHelper ExamplesTable?
ExamplesTable! := #TableRow #TableRow*

Step! := #StepLine StepArg?
StepArg := (DataTable | DocString)

DataTable! := #TableRow+
DocString! := #DocStringSeparator #Other* #DocStringSeparator

Tags! := #TagLine+

// descriptions should be allowed to be interspersed with comments and empty lines
// if only comments and empty lines are encountered, the description text will be empty
DescriptionHelper := Description?
Description! := (#Empty | #Comment | #Other)+
